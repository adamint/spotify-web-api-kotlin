buildscript {
    ext.kotlin_version = '1.3.31'
    ext.dokka_version = '0.9.17'

    repositories { jcenter() }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}


plugins {
    id "com.diffplug.gradle.spotless" version "3.23.0"
    id "base"
    id "io.codearte.nexus-staging" version "0.20.0"
    id "com.bmuschko.nexus" version "2.3.1"
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

group 'com.adamratzman'
version '2.1.3'

archivesBaseName = 'spotify-api-kotlin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    // Actual library dependencies
    compile 'com.beust:klaxon:5.0.5'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    
    // Spek testing requirements
    testCompile('org.spekframework.spek2:spek-dsl-jvm:2.0.0') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntimeOnly('org.spekframework.spek2:spek-runner-junit5:2.0.0') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
    
    testCompile('org.junit.jupiter:junit-jupiter-api:5.4.2')
    testCompile(group: 'org.json', name: 'json', version: '20180130')
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = 'docs'

    jdkVersion = 8
    includeNonPublic = false
    impliedPlatforms = ["JVM"]

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/adamint/spotify-web-api-kotlin/tree/master/src/main/kotlin"
        suffix = "#L"
    }
}

spotless {
    kotlin {
        ktlint()
        licenseHeader '/* Created by Adam Ratzman (2018) */'    // License header
    }
}

test {
    systemProperties project.properties.subMap(
            ["clientId", "clientSecret", "spotifyTokenString", "spotifyRedirectUri"]
    )
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

modifyPom {
    project {
        name 'spotify-api-kotlin'
        description 'A Kotlin wrapper for the Spotify Web API.'
        url 'https://github.com/adamint/spotify-web-api-kotlin'
        inceptionYear '2018'
        scm {
            url 'https://github.com/adamint/spotify-web-api-kotlin'
            connection 'scm:https://github.com/adamint/spotify-web-api-kotlin.git'
            developerConnection 'scm:git://github.com/adamint/spotify-web-api-kotlin.git'
        }
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'adamratzman'
                name 'Adam Ratzman'
                email 'adam@adamratzman.com'
            }
        }
    }
}
extraArchive {
    sources = true
    tests = true
    javadoc = true
}
nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

nexusStaging {
    packageGroup = "com.adamratzman"
}
