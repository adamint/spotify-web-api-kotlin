buildscript {
    apply from: "$rootDir/gradle/dependencies.gradle"

    repositories { jcenter() }

    dependencies {
        classpath deps.plugins.kotlin
        classpath deps.plugins.dokka
    }
}

plugins {
    id "io.codearte.nexus-staging" version "0.21.0"
    id "base"
    id "com.diffplug.gradle.spotless" version "3.24.0"
}
apply plugin: 'org.jetbrains.dokka'
apply from: "$rootDir/gradle/dependencies.gradle"

allprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.dokka'
    repositories {
        jcenter()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    group 'com.adamratzman'
    version '3.0.00'
}

dokka {
    kotlinTasks {
        defaultKotlinTasks() + [':core:compileKotlin', ':extensions:coroutine:compileKotlin']
    }
    outputFormat = 'javadoc'
    outputDirectory = 'docs'

    jdkVersion = 8
    includeNonPublic = false
    impliedPlatforms = ["JVM"]
}

spotless {
    kotlin {
        target '**/*.kt'
        ktlint()
        licenseHeader '/* Spotify Web API - Kotlin Wrapper; MIT License, 2019; Original author: Adam Ratzman */'    // License header
    }
}

nexusStaging {
    packageGroup = "com.adamratzman"
}
