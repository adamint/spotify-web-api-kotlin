plugins {
    id "com.diffplug.gradle.spotless" version "3.16.0"
    id "base"
    id "io.codearte.nexus-staging" version "0.12.0"
    id "com.bmuschko.nexus" version "2.3.1"
    id "org.jetbrains.kotlin.jvm" version "1.3.11"
}

group 'com.adamratzman'
version '2.0.0'

archivesBaseName = 'spotify-api-kotlin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://dl.bintray.com/spekframework/spek" }
}

dependencies {
    // Actual library dependencies
    implementation(group: 'org.json', name: 'json', version: '20180130')
    implementation('org.jsoup:jsoup:1.10.3')
    implementation("com.google.code.gson:gson:2.8.1")

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.11"
    compile "org.jetbrains.kotlin:kotlin-reflect:1.3.11"

    // Spek testing requirements
    testImplementation('org.spekframework.spek2:spek-dsl-jvm:2.0.0-rc.1') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntimeOnly('org.spekframework.spek2:spek-runner-junit5:2.0.0-rc.1') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }

    testImplementation('org.junit.jupiter:junit-jupiter-api:5.2.0')
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:1.3.11"
}

spotless {
    kotlin {
        ktlint()
        licenseHeader '/* Created by Adam Ratzman (2018) */'    // License header
    }
}


test {
    systemProperties project.properties.subMap(
            ["clientId", "clientSecret", "spotifyTokenString", "spotifyRedirectUri"]
    )
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

modifyPom {
    project {
        name 'spotify-api-kotlin'
        description 'A Kotlin wrapper for the Spotify Web API.'
        url 'https://github.com/adamint/spotify-web-api-kotlin'
        inceptionYear '2018'
        scm {
            url 'https://github.com/adamint/spotify-web-api-kotlin'
            connection 'scm:https://github.com/adamint/spotify-web-api-kotlin.git'
            developerConnection 'scm:git://github.com/adamint/spotify-web-api-kotlin.git'
        }
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'adamratzman'
                name 'Adam Ratzman'
                email 'adam@adamratzman.com'
            }
        }
    }
}
extraArchive {
    sources = true
    tests = true
    javadoc = true
}
nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

nexusStaging {
    packageGroup = "com.adamratzman"
}
